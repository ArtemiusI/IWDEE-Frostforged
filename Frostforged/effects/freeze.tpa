ACTION_IF NOT FILE_EXISTS_IN_GAME ~C0FREEZE.SPL~ BEGIN

COPY ~%MOD_FOLDER%/effects/freeze~ ~override~

DEFINE_ACTION_FUNCTION	ADD_SPLPROT	INT_VAR	stat = 0	value = 0	relation = 0	RET	new_row		BEGIN
	ACTION_IF	(stat > 278)	BEGIN	WARN	~SPLPROT.2DA - Stat out of Bounds.~	END
	ACTION_IF	((stat != 259) AND (stat != 260) AND (relation > 11))	BEGIN	WARN	~SPLPROT.2DA - Relation out of Bounds.~	END
	COPY_EXISTING ~SPLPROT.2DA~	~override~
		COUNT_2DA_COLS	cols
		COUNT_2DA_ROWS cols rows
		TEXT_SPRINT check_row ~~
		TEXT_SPRINT check_stat ~~
		SET new_row = 0
		FOR(current_row = 0; current_row < rows; ++current_row)	BEGIN	READ_2DA_ENTRY current_row 1 cols check_stat
			PATCH_IF (~%check_stat%~ STRING_EQUAL ~%stat%~)	BEGIN	READ_2DA_ENTRY current_row 2 cols check_value
				PATCH_IF (~%check_value%~ STRING_EQUAL ~%value%~)	BEGIN	READ_2DA_ENTRY current_row 3 cols check_relation
					PATCH_IF (~%check_relation%~ STRING_EQUAL ~%relation%~)	BEGIN	new_row = current_row	current_row = rows	END
				END
			END
		END
		PATCH_IF (new_row = 0)	BEGIN
			FOR(current_row = 0; current_row < rows; ++current_row)	BEGIN	READ_2DA_ENTRY current_row 1 cols check_stat
				PATCH_IF (~%check_stat%~ STRING_EQUAL ~*~)	BEGIN	new_row = current_row	current_row = rows	END
			END
			PATCH_IF (new_row = 0)	BEGIN	new_row = rows	INSERT_2DA_ROW rows cols ~%new_row%		   %stat%		   %value%		   %relation%~
			END	ELSE	BEGIN	SET_2DA_ENTRY new_row 1 cols ~%stat%~	SET_2DA_ENTRY new_row 2 cols ~%value%~	SET_2DA_ENTRY new_row 3 cols ~%relation%~	END
		END
	BUT_ONLY
END
//	Array of Restistance / Stat#
ACTION_DEFINE_ASSOCIATIVE_ARRAY	DMG_ARRAY	BEGIN
	~CRUSH~		=>	22
	~ACID~		=>	17
	~COLD~		=>	15
	~ELEC~		=>	16
	~FIRE~		=>	14
	~PIERC~		=>	23
	~POISN~		=>	74
	~MAGIC~		=>	73
	~MSSL~		=>	24
	~SLASH~		=>	21
	~MFIRE~		=>	19
	~MCOLD~		=>	20
END
ACTION_PHP_EACH	DMG_ARRAY AS one => two	BEGIN
	LAF	ADD_SPLPROT	INT_VAR	stat = two	value = 100	relation = 	2	RET	min = new_row	END
	LAF	ADD_SPLPROT	INT_VAR	stat = two	value = 128	relation = 	4	RET	max = new_row	END
	LAF	ADD_SPLPROT	INT_VAR	stat = 260	value = min	relation = 	max	RET	EVAL ~%one%~ = new_row	END
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY	DMG_ARRAY2	BEGIN
	~FIRE2~		=>	14
END
ACTION_PHP_EACH	DMG_ARRAY2 AS one => two	BEGIN
	LAF	ADD_SPLPROT	INT_VAR	stat = two	value = 0	relation = 	2	RET	min = new_row	END
	LAF	ADD_SPLPROT	INT_VAR	stat = two	value = 128	relation = 	4	RET	max = new_row	END
	LAF	ADD_SPLPROT	INT_VAR	stat = 260	value = min	relation = 	max	RET	EVAL ~%one%~ = new_row	END
END

DEFINE_ACTION_FUNCTION cd_new_portrait_icon
  INT_VAR string = 0
  STR_VAR bam_file = "****"
  RET     icon
  BEGIN

  COPY_EXISTING ~statdesc.2da~ ~override~
    COUNT_2DA_ROWS 3 count
    READ_2DA_ENTRY (count - 1) 0 3 icon
    SET icon += 1

  APPEND ~statdesc.2da~ ~%icon% %string% %bam_file%~ UNLESS ~%bam_file%~

END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(~Frozen~) STR_VAR bam_file = c0aicon1 RET c0aicon1 = icon END

COPY_EXISTING ~c0autt02.spl~ ~override~
  LPF ALTER_EFFECT
    INT_VAR
      check_globals = 1
      check_headers = 1
      match_opcode = 139 // display string
	  parameter1 = RESOLVE_STR_REF (~Shattered~)
  END
COPY_EXISTING ~c0freeze.spl~ ~override~
		LPF	ADD_SPELL_EFFECT
			INT_VAR
				insert_point = 0
				opcode = 318
				target = 2
				parameter2 = COLD
				duration = 1
			STR_VAR
				resource = EVAL ~%SOURCE_RES%~
		END
      LPF ALTER_EFFECT
      INT_VAR
        check_globals     = 0
        check_headers     = 1
        match_opcode      = 142   // Display portrait icon
        match_parameter2  = 82    // Called Shot icon
        parameter2        = c0aicon1
      END
  LPF ALTER_EFFECT
    INT_VAR
      check_globals = 1
      check_headers = 1
      match_opcode = 139 // display string
	  parameter1 = RESOLVE_STR_REF (~Frozen~)
  END

END